>>>malloy
source: amazon_purchases_data is duckdb.table('amazon_purchases.csv') extend {
dimension: survey_response_id is `Survey ResponseID`
dimension: order_date is `Order Date`
dimension: purchase_price_per_unit is `Purchase Price Per Unit`
dimension: quantity is `Quantity`
dimension: shipping_state is `Shipping Address State`
dimension: product_title is `Title`
dimension: product_code is `ASIN/ISBN (Product Code)`

dimension: category is `Category`
measure: total_spent is sum(purchase_price_per_unit * quantity)
measure: total_quantity is sum(quantity)
primary_key: survey_response_id
}
source: survey_data is duckdb.table('survey.csv') extend {
dimension: survey_response_id is `Survey ResponseID`
dimension: shopping_frequency is `Q-amazon-use-how-oft`
dimension: age is `Q-demos-age`
join_one: amazon_purchases_data with survey_response_id
}
>>>markdown
What is the Amazon shopping frequency distribution across different age groups?
>>>malloy
# bar_chart
run: survey_data -> {
  group_by: age, shopping_frequency
  aggregate: response_count is count()
  order_by: response_count desc
}
>>>markdown
Which states have the highest total spending on Amazon?
>>>malloy
# shape_map
run: amazon_purchases_data -> {
  group_by: shipping_state
  where: shipping_state is not null
  aggregate: total_spent 
  order_by: total_spent desc
  limit: 5
}
>>>markdown
What are the most purchased product categories?
>>>malloy
# bar_chart
run: amazon_purchases_data -> {
  where: category is not null
  group_by: category
  aggregate: total_quantity is sum(quantity)
  order_by: total_quantity desc
  limit: 5
}
>>>markdown
How does shopping frequency vary across education levels?
>>>malloy
# bar_chart
run: survey_data -> {
  group_by: `Q-demos-education`, shopping_frequency
  aggregate: response_count is count()
  order_by: response_count desc
}
>>>markdown
What are the most common life changes reported by Amazon shoppers?
>>>malloy
# bar_chart
run: survey_data -> {
  group_by: `Q-life-changes`
  aggregate: change_count is count()
  order_by: change_count desc
  limit: 10
}
>>>markdown
What percentage of total Amazon spending comes from each state?
>>>malloy
run: amazon_purchases_data -> {
  group_by: shipping_state
  aggregate: total_spent 
  # percent
  percent_of_total_spent is total_spent / all(total_spent)
  order_by: total_spent desc
  limit: 10
}
>>>markdown
What percentage of gift card purchases come from each state?
>>>malloy

run: amazon_purchases_data -> {
  where: category = 'GIFT_CARD' and shipping_state is not null
  group_by: shipping_state
  aggregate: 
    total_gift_card_purchases is count()
    # percent
    percent_of_total_gift_card_purchases is count() / all(count())
  order_by: total_gift_card_purchases desc
  limit: 10
}
>>>markdown
What is the most common life change reported in each state?
>>>malloy

run: survey_data -> {
  group_by: `Q-demos-state`
    # bar_chart
  nest: top_life_change is {
    where: `Q-life-changes` is not null
    group_by: `Q-life-changes`
    aggregate: change_count is count()
    order_by: change_count desc
    limit: 3
  }

}
>>>markdown
What is the most common reason for life change in Washington state?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-state`, `Q-life-changes`
    where: `Q-life-changes` is not null and `Q-demos-state` = 'Washington'
    aggregate: change_count is count()
    order_by: change_count desc
  }
>>>markdown
Which states have the highest number of frequent Amazon shoppers (who shop more than 10 times per month), while ignoring NULL values?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-state`
  nest: high_frequency_shoppers is {
    where: `Q-amazon-use-how-oft` = "More than 10 times per month" and `Q-demos-state` is not null 
    aggregate: shopper_count is count()
    order_by: shopper_count desc
  }
}
>>>markdown
What is the shopping frequency for different age groups?
>>>malloy
run: survey_data -> {
  group_by: age, shopping_frequency 
  aggregate: response_count is count()
  order_by: response_count  desc
}
>>>markdown
How many survey responses were collected for each income level?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-income`
  aggregate: response_count is count()
  order_by: response_count desc
}
>>>markdown
What is most common income level for each education level?
>>>malloy

run: survey_data -> {
  group_by: `Q-demos-education`

  nest: top_income_level is {
    where: `Q-demos-income` is not null
    group_by: `Q-demos-income`
    aggregate: income_count is count()
    order_by: income_count desc
    limit: 1
  }
}
>>>markdown
Which states have the highest number of Amazon shoppers with an income of $150,000 or more?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-state`
  nest: high_income_shoppers is {
    where: `Q-demos-income` = "$150,000 or more" 
    and `Q-demos-state` is not null
    aggregate: shopper_count is count()
    order_by: shopper_count desc
  }
  
}
>>>markdown
How do peopleâ€™s education levels influence their willingness to sell their personal data?
>>>malloy

run: survey_data -> {
  group_by: `Q-demos-education`
 
  nest: willing_to_sell_data is {
    where: `Q-sell-YOUR-data` in ("Yes", "Yes if I get part of the profit")
    and `Q-demos-education` is not null
    aggregate: response_count is count()
    order_by: response_count desc
  }
}
>>>markdown
Do frequent Amazon shoppers have different substance use behaviors compared to less frequent shoppers?
>>>malloy
run: survey_data -> {
  group_by: shopping_frequency
  nest: substance_use is {
    select: `Q-substance-use-cigarettes`, `Q-substance-use-alcohol`, `Q-substance-use-marijuana`
    limit:3
  }
}
>>>markdown
How do shoppers with different education levels feel about selling consumer data?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-education`, `Q-sell-consumer-data`
  aggregate: response_count is count()
  order_by: response_count desc
  limit: 5
}
>>>markdown
Which states have the highest number of Amazon shoppers who moved recently?
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-state`
  where: `Q-life-changes` = 'Moved place of residence'
  aggregate: response_count is count()
  order_by: response_count desc
  limit: 10
}
>>>markdown
What is average Spending by Age Group
>>>malloy
run: survey_data -> {
  group_by: `Q-demos-age`
  aggregate: avg_spent is avg(amazon_purchases_data.purchase_price_per_unit * amazon_purchases_data.quantity)
  order_by: avg_spent desc
}
>>>markdown
What is the spending habits based on major life changes?
>>>malloy
run: survey_data -> {
  where: `Q-life-changes` is not null
  group_by: `Q-life-changes`
  aggregate:
    total_spending is sum(amazon_purchases_data.purchase_price_per_unit * amazon_purchases_data.quantity)
  order_by: total_spending desc
}
